

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'



android {

    compileSdkVersion 31

    defaultConfig {
        applicationId "de.jadehs.vcg"
		
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 21
        versionName "0.4.2"



        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "int", "DATABASE_DATA_VERSION", "3"
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    buildFeatures{
        viewBinding true
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation(group: 'com.graphhopper', name: 'graphhopper-core', version: '1.0') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'org.openstreetmap.osmosis', module: 'osmosis-osm-binary'
        exclude group: 'org.apache.xmlgraphics', module: 'xmlgraphics-commons'
    }


    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.activity:activity:1.2.0'
    implementation 'androidx.fragment:fragment:1.3.0'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'


    def androidXTestVersion = "1.4.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test:runner:$androidXTestVersion"
    androidTestImplementation "androidx.test:rules:$androidXTestVersion"
    // Optional -- UI testing with Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    implementation "androidx.core:core:1.8.0"


    implementation "androidx.media:media:1.1.0"



    implementation "io.reactivex.rxjava3:rxjava:3.1.4"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"

    // nav graph

    def nav_version = "2.3.5"

    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"


    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"


    //exoplayer

    def exo_version = "2.18.0"

    implementation "com.google.android.exoplayer:exoplayer:$exo_version"
    implementation "com.google.android.exoplayer:extension-mediasession:$exo_version"

    implementation 'com.github.deano2390:FlowTextView:2.0.5'

    // photoview
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'


    // room
    def room_version = "2.4.2"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // coroutines
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0"

    def lifecycle_version = "2.2.0"

    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"

    // Kotlin

    implementation "androidx.core:core-ktx:1.3.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Map
    def vtm_version = "0.18.0"
    implementation "org.mapsforge:vtm:$vtm_version"
    implementation "org.mapsforge:vtm-themes:$vtm_version"
    implementation 'net.sf.kxml:kxml2:2.3.0'
    implementation 'org.slf4j:slf4j-api:1.7.28'


    implementation "org.mapsforge:vtm-android:$vtm_version:natives-armeabi-v7a"
    implementation "org.mapsforge:vtm-android:$vtm_version:natives-arm64-v8a"
    implementation "org.mapsforge:vtm-android:$vtm_version:natives-x86"
    implementation "org.mapsforge:vtm-android:$vtm_version:natives-x86_64"
    implementation "org.mapsforge:vtm-android:$vtm_version"
    implementation "com.caverock:androidsvg:1.4"

    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'


    // Play Services
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    // Markdown
    implementation "io.noties.markwon:core:4.6.2"

    implementation 'de.hdodenhof:circleimageview:3.1.0'





}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task createDB (type:Exec){

    workingDir rootDir
    inputs.files(rootDir.toString()+'/Database/Build/buildDatabase.py',project.android.defaultConfig.javaCompileOptions.annotationProcessorOptions.arguments["room.schemaLocation"],rootDir.toString()+'/Database/Build/insert.yml')
    outputs.file('src/main/assets/TestData.db')
    commandLine 'python','Database/Build/buildDatabase.py',project.android.defaultConfig.javaCompileOptions.annotationProcessorOptions.arguments["room.schemaLocation"],'Database/Build/insert.yml','app/src/main/assets/TestData.db'
}
task deleteUserData(){
    inputs.files(rootDir.toString()+'\\Database\\Build\\buildDatabase.py',project.android.defaultConfig.javaCompileOptions.annotationProcessorOptions.arguments["room.schemaLocation"],rootDir.toString()+'\\Database\\Build\\insert.yml')
    doLast{
        exec{
            commandLine 'adb', 'shell', 'pm', 'clear' ,project.android.defaultConfig.applicationId
        }
    }
}

afterEvaluate {
    compileDebugJavaWithJavac.finalizedBy(createDB)
}

repositories {
    mavenCentral()
}